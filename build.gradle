buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = "https://plugins.gradle.org/m2" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.60'
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: "org.spongepowered.mixin"
apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'java'
compileJava.options.encoding = 'UTF-8'

group = 'com.cifrazia'
version = '0.0.1'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version


    accessTransformer = file('src/main/resources/META-INF/custom_main_menu_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker', '--mixin', 'custom_main_menu.mixins.json'
        }

        server {
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker', '--mixin', 'custom_main_menu.mixins.json'
        }
    }
}

mixin {
    add sourceSets.main, "custom_main_menu.mixins.refmap.json"
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
}


dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    implementation "org.spongepowered:mixin:0.8.5-SNAPSHOT"
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
}


sourceSets {
    main {
        output.resourcesDir = output.classesDirs.singleFile
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching(["mcmod.info", "pack.mcmeta"]) {
        expand project.properties
    }
}

jar {
    manifest {
        attributes([
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs": "custom_main_menu.mixins.json",
                "FMLCorePluginContainsFMLMod": "true", // Не забываем указать эти два флага,
                "ForceLoadAsMod": "true",              // чтобы Forge нормально загрузил и мод, и миксины
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLAT": "custom_main_menu_at.cfg"
        ])
    }
}






